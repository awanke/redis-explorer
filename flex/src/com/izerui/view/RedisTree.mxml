<?xml version="1.0" encoding="utf-8"?>
<!--
  Created by Administrator on 2015/2/4.
-->
<support:DoubleClickTree xmlns:fx="http://ns.adobe.com/mxml/2009"
                         xmlns:support="com.izerui.support.*"
                         labelField="displayName"
                         treeItemClick="tree_treeItemClickHandler(event)"
                         creationComplete="tree1_creationCompleteHandler(event)">

    <fx:Metadata>
        [Event(name="dbItemSelectEvent",type="com.izerui.support.event.DbItemSelectEvent")]
    </fx:Metadata>


    <fx:Script><![CDATA[
        import com.adobe.cairngorm.control.CairngormEventDispatcher;
        import com.izerui.event.GetDbAmountEvent;
        import com.izerui.event.GetServerConfigsEvent;
        import com.izerui.support.event.DbItemSelectEvent;
        import com.izerui.support.event.TreeItemClickEvent;
        import com.izerui.vo.RedisServerConfig;

        import mx.collections.ArrayCollection;
        import mx.events.FlexEvent;

        public function loadTreeData():void {
            CairngormEventDispatcher.getInstance().dispatchEvent(new GetServerConfigsEvent(function (data:ArrayCollection):void {
                dataProvider = data;
            }));
        }

        private function tree1_creationCompleteHandler(event:FlexEvent):void {
            loadTreeData();
        }


        private function tree_treeItemClickHandler(event:TreeItemClickEvent):void {
            var selItem:RedisServerConfig = RedisServerConfig(selectedItem);
            if (selItem.children) {
                return;
            }
            if (selItem.dbIndex == -1) {
                CairngormEventDispatcher.getInstance().dispatchEvent(new GetDbAmountEvent(selItem, function (data:int):void {
                    selItem.children = new ArrayCollection();
                    for (var i:int = 0; i < data; i++) {
                        var dbConfig:RedisServerConfig = new RedisServerConfig();
                        dbConfig.id = selItem.id;
                        dbConfig.host = selItem.host;
                        dbConfig.port = selItem.port;
                        dbConfig.password = selItem.password;
                        dbConfig.createTime = selItem.createTime;
                        dbConfig.updateTime = selItem.updateTime;
                        dbConfig.displayName = "db" + i;
                        dbConfig.dbIndex = i;
                        selItem.children.addItem(dbConfig);
                    }
                    expandItem(selItem, true, true);
                }));
                return;
            }
            //sel db0 ~ db15
            if (selItem.dbIndex > -1) {
                this.dispatchEvent(new DbItemSelectEvent(selItem))
            }

        }
        ]]>
    </fx:Script>

</support:DoubleClickTree>
